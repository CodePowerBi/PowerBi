UK_12M =
VAR MesSlicer =
    SELECTEDVALUE ( 'MesSelector'[MesAnioOrden] )

VAR MesActualCodigo =
    SELECTEDVALUE ( EjeMes_12M[MesAnioOrden] )

VAR MesConvertido =
    IF (
        NOT ISBLANK ( MesSlicer ),
        DATE ( INT ( MesSlicer / 100 ), MOD ( MesSlicer, 100 ), 1 )
    )

VAR MesActualFecha =
    IF (
        NOT ISBLANK ( MesActualCodigo ),
        DATE ( INT ( MesActualCodigo / 100 ), MOD ( MesActualCodigo, 100 ), 1 )
    )

VAR DentroVentana =
    NOT ISBLANK ( MesConvertido )
        && NOT ISBLANK ( MesActualFecha )
        && MesActualFecha <= MesConvertido
        && MesActualFecha > EDATE ( MesConvertido, -12 )

VAR MaxEffectiveDate =
    CALCULATE (
        MAX ( lcr_historico[effective_date] ),
        ALL ( lcr_historico )
    )

RETURN
IF (
    NOT DentroVentana,
    BLANK (),
    CALCULATE (
        SUM ( lcr_historico[brazil] ),

        -- Forzamos Ãºltimo snapshot
        lcr_historico[effective_date] = MaxEffectiveDate,

        KEEPFILTERS (
            FILTER (
                ALL ( lcr_historico ),
                lcr_historico[fecha] >= MesActualFecha
                    && lcr_historico[fecha] < EOMONTH ( MesActualFecha, 0 ) + 1
            )
        )
    )
)
